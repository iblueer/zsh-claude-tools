# zsh completion for claude-switch

if ! type compinit >/dev/null 2>&1; then
  autoload -Uz compinit
  compinit
fi

: ${CLAUDE_CODE_HOME:="$HOME/.claude"}
local _cu_envdir="${CLAUDE_CODE_HOME}/envs"

_cu_list_envs() {
  local -a envs
  envs=(${_cu_envdir}/*.env(N:t:r))
  print -l -- $envs
}

_claude-switch() {
  local -a subs
  subs=(
    'list:列出所有环境'
    'use:切换到指定环境'
    'new:新建环境'
    'edit:编辑环境'
    'del:删除环境'
    'show:显示已记忆与当前变量'
    'current:同 show'
    'help:显示帮助'
    'open:打开环境目录'
    'dir:打开环境目录（同 open）'
  )

  local -a envs
  envs=($(_cu_list_envs))

  if (( CURRENT == 2 )); then
    _describe -t subcmds '子命令' subs
    compadd -X '环境（来自 envs 目录）' -a envs
    return
  fi

  if (( CURRENT == 3 )); then
    case ${words[2]} in
      use|edit|del|delete|rm)
        compadd -a envs
        return
        ;;
      new)
        return
        ;;
    esac
  fi
}

compdef _claude-switch claude-switch
